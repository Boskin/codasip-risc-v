#include "config.hcodal"
#include "ca_defines.hcodal"
#include "opcodes.hcodal"
#include "debug.hcodal"

event ex : pipeline(pipe.EX) {
    use ex_output;
    use ex_branch;

    semantics {
        // Check for HALT
        if(OPCODE(r_ex_instr) == OPC_HALT) {
            codasip_halt();
            codasip_info(INFO_GENERAL, "Halt: return value=%d\n",
                rf_xpr[XPR_RET_ARG] & 0xff);
            codasip_store_exit_code(rf_xpr[XPR_RET_ARG] & 0xff);
        }

        // Stalling logic
        s_ex_stall = s_me_stall;

        // Select the value for opperand a
        switch(r_ex_src_a_mux) {
        case SRC_A_XPR:
            s_ex_alu_in_a = r_ex_xpr_data_a;
            break;

        case SRC_A_PC:
            s_ex_alu_in_a = r_ex_pc;
            break;

        case SRC_A_ZERO:
            s_ex_alu_in_a = 0;
            break;

        default:
            s_ex_alu_in_a = r_ex_xpr_data_a;
            codasip_assert(false, "Default case in ex_src_a in ex: %d",
                r_ex_src_a_mux);
        }

        // Select the value for opperand b
        switch(r_ex_src_b_mux) {
        case SRC_B_XPR:
            s_ex_alu_in_b = r_ex_xpr_data_b;
            break;

        case SRC_B_IMM:
            s_ex_alu_in_b = r_ex_imm;
            break;

        case SRC_B_INSTR_SIZE:
            s_ex_alu_in_b = RISCV_INSTR_SIZE;
            break;

        default:
            s_ex_alu_in_b = r_ex_xpr_data_b;
            codasip_assert(false, "Default case in event ex in src b mux: %d",
                r_ex_src_b_mux);
        }

        // Evaluate the desired ALU operation
        switch(r_ex_alu_op) {
        case ALU_ADD:
            s_ex_alu_out = (int32)s_ex_alu_in_a + (int32)s_ex_alu_in_b;
            break;

        case ALU_SUB:
            s_ex_alu_out = (int32)s_ex_alu_in_a - (int32)s_ex_alu_in_b;
            break;

        case ALU_AND:
            s_ex_alu_out = s_ex_alu_in_a & s_ex_alu_in_b;
            break;

        case ALU_OR:
            s_ex_alu_out = s_ex_alu_in_a | s_ex_alu_in_b;
            break;

        case ALU_XOR:
            s_ex_alu_out = s_ex_alu_in_a ^ s_ex_alu_in_b;
            break;

        case ALU_SEQ:
            s_ex_alu_out = s_ex_alu_in_a == s_ex_alu_in_b;
            break;

        case ALU_SLL:
            s_ex_alu_out = s_ex_alu_in_a << (uint5)s_ex_alu_in_b;
            break;

        case ALU_SRL:
            s_ex_alu_out = (uint32)s_ex_alu_in_a >> (uint5)s_ex_alu_in_b;
            break;

        case ALU_SRA:
            s_ex_alu_out = (int32)s_ex_alu_in_a >> (uint5)s_ex_alu_in_b;
            break;

        case ALU_SLT:
            s_ex_alu_out = (int32)s_ex_alu_in_a < (int32)s_ex_alu_in_b;
            break;

        case ALU_SLTU:
            s_ex_alu_out = ((uint32)s_ex_alu_in_a) < (uint32)s_ex_alu_in_b;
            break;

        case ALU_SGE:
            s_ex_alu_out = (int32)s_ex_alu_in_a >= (int32)s_ex_alu_in_b;
            break;

        case ALU_SGEU:
            s_ex_alu_out = (uint32)s_ex_alu_in_a >= (uint32)s_ex_alu_in_b;
            break;

        default:
            s_ex_alu_out = 0;
            codasip_assert(false, "Default case in event ex in alu op: %d",
               r_ex_alu_op);
        }
    };

    timing {
        ex_branch();
        ex_output();
    };
};

event ex_branch : pipeline(pipe.EX) {
    semantics {
        // Compute the target jump/branch address
        s_ex_branch_target = (r_ex_br_alu_src_mux == BR_ALU_PC ? r_ex_pc :
            r_ex_xpr_data_a) + r_ex_imm;
        // Determine if the operation is 0
        s_ex_zero = s_ex_alu_out == (int32)0;
        // Determine whether to take the jump or not
        s_ex_take_branch = (s_ex_zero && (r_ex_jump_op == JUMP_COND)) ||
            (r_ex_jump_op == JUMP_TRUE);
    };
};

event ex_output : pipeline(pipe.EX) {
    semantics {
        // Load the ME registers
        r_me_pc = r_ex_pc;
        r_me_instr = r_ex_instr;
        r_me_xpr_dest = r_ex_xpr_dest;
        r_me_xpr_dest_src = r_ex_xpr_dest_src;
        r_me_mem_op = r_ex_mem_op;
        r_me_xpr_b = r_ex_xpr_b;
        r_me_alu_data_b = r_ex_xpr_data_b;
        r_me_alu_out = s_ex_alu_out;
    };
};
